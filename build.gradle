plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.3'
    id "io.freefair.lombok" version "6.5.1"
}

loom {
    // im not sure what this does,
    // but i think we might need it
    runConfigs.configureEach {
        ideConfigGenerated = true
    }
}

allprojects {
    apply plugin: "fabric-loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"
    apply plugin: "io.freefair.lombok"

    version = project.mod_version
    group = project.maven_group

    repositories {
        maven {
            url "https://maven.uku3lig.net/releases"
        }
        maven {
            url "https://maven.uku3lig.net/snapshots"
        }
        maven {
            url "https://maven.terraformersmc.com/releases"
        }
    }

    dependencies {
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 8
    }

    processResources {
        inputs.property "version", project.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}" }
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modCompileOnly "net.uku3lig:ukulib:${project.ukulib_version}"
    modCompileOnly fabricApi.module("fabric-key-binding-api-v1", project.fabric_version)
    modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"

    subprojects.forEach((project) -> {
        include(project) {
            transitive = false
        }
    })
}

subprojects {
    dependencies {
        // include the root project because that's where our common code is
        implementation(project(path: ":", configuration: "namedElements")) {
            // having 2 fabric loaders from the sub project and root project causes issues so we get rid of one.
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
}
